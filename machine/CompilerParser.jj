options {
    STATIC = false;
}

PARSER_BEGIN(CompilerParser)

package machine;


import java.util.List;
import java.util.Map;
import java.util.ArrayList;
import java.util.HashMap;


public class CompilerParser {

    private boolean preflight = false;

    private List<Number> byteCode = new ArrayList<>();
    private CodeSegment codeSegment = new CodeSegment();
    private DataSegment dataSegment = new DataSegment();

    private List<String> macroVariables = new ArrayList<>();

    public static void main(String[] args) throws ParseException {
        CompilerParser parser = new CompilerParser(System.in);
        parser.Program(false);
    }

    String getImage( Token t ) {
        StringBuilder sb = new StringBuilder();
        for( Token tt = t; tt != null; tt = tt.next ) {
            sb.append(tt.image);
        }
        return sb.toString();
    }

}

PARSER_END(CompilerParser)

SKIP :
{
  " "
| "\t"
| "\r"
| "\n"
}


TOKEN : {
    < HALT : "HALT" >
  | < NOP : "NOP" >
  | < INC : "INC" >
  | < DEC : "DEC" >
  | < JNZ : "JNZ" >
  | < VAR : "var" >
  | < ASSIGN : "=" >
  | < DO : "do" >
  | < WHILE : "while" >
  | < IF : "if" >
  | < ELSE : "else" >
  | < VOID : "void" >
  | < IDENTIFIER : ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* >
  | < NUMBER : ( ["0"-"9"] )+ >
  | < NEQ : "!=" >
  | < EQ : "==" >
  | < COLON : ":" >
  | < EOS : ";" >
  | < BEGIN : "begin:" >
}

List<Number> Program( boolean preflight ) : {
} {
    {
        this.preflight = preflight;
        dataSegment.add(codeSegment.newLabel("begin"));
    }
    ( MacroDefinition() )*
    ( GlobalVariableDeclaration() <EOS> )*
    <BEGIN> {
        codeSegment.placeLabel("begin");
    }
    StatementsOrBlocks() 
    <EOF> {
        byteCode.addAll(dataSegment);
        codeSegment.applyOffset( byteCode.size() );
        byteCode.addAll(codeSegment);
        return byteCode;
    }
}

void MacroDefinition() : {
}{
    <VOID> MacroSignature() "{"
            StatementsOrBlocks()
    "}" {
        macroVariables.clear();
    }
}

void GlobalVariableDeclaration() : {
    Token id, rvalue, size, index;
    int length = 1;
    int offset = 0;
} {
    <VAR> id = <IDENTIFIER> ( "[" size = <NUMBER> "]" { length = Integer.parseInt(size.image); } )? <ASSIGN> (
          rvalue = <NUMBER> {
            Number variable = dataSegment.newVariable(id.image, length, rvalue.image);
        }
        | rvalue = <IDENTIFIER> ( "[" index = <NUMBER> "]" { offset = Integer.parseInt(index.image); } )? {
            Number right  = dataSegment.getVariable(rvalue.image, offset);
            if( right == null ) {
                throw new ParseException("Unknown variable " + rvalue.image + " in line " + rvalue.beginLine );
            }
            Number variable = dataSegment.newVariable(id.image, length, right.getValue());
        }
    )
}


void StatementsOrBlocks() : {
} {    
    ( StatementOrBlock() )*
}

void StatementOrBlock() : {
} {
    LOOKAHEAD(2) UnlabeledStatementOrBlock() | ( LOOKAHEAD(2) LabelDeclaration() )+ UnlabeledStatementOrBlock()
}


void LabelDeclaration() : {
    Token label;
} {
    label = <IDENTIFIER> <COLON> {
        codeSegment.placeLabel(label.image);
    }
}

void UnlabeledStatementOrBlock() : {
} {
      Block()
    | DoWhileBlock()
    | WhileBlock()
    | IfElseBlock()
    | Statement()
}

void Block() : {
} {
    "{" StatementsOrBlocks() "}"
}

void DoWhileBlock() : {
    Number condition;
    Number target = codeSegment.newLabel();
} {
    <DO> "{"
        StatementsOrBlocks()
    "}" <WHILE> "(" condition = DoWhileCondition() ")" <EOS> {
        codeSegment.addJNZ(condition, target);
    }
}

void WhileBlock() : {
    Number condition;
    Number begin = codeSegment.newLabel();
    Number end = codeSegment.newLabel();
} {
    <WHILE> "(" condition=IfCondition() ")" { codeSegment.addJNZ(condition, end); } "{"
        StatementsOrBlocks()
    "}" {
        codeSegment.addJNZ(new Number(0), begin);
        codeSegment.placeLabel(end);
    }
}

void IfElseBlock() : {
    Number condition;
    Number elseLabel = codeSegment.newLabel();
    Number endLabel = codeSegment.newLabel();
    boolean hasElse = false;
} {
    <IF> "(" condition=IfCondition() ")" { codeSegment.addJNZ(condition, elseLabel); } "{"
        StatementsOrBlocks()
    "}" ( <ELSE> {
        codeSegment.addJNZ(new Number(0), endLabel);
        codeSegment.placeLabel(elseLabel);
        hasElse = true;
    } "{"
        StatementsOrBlocks()
    "}" )? {
        codeSegment.placeLabel(endLabel);
    }
    {
        if( !hasElse ) {
            codeSegment.placeLabel(elseLabel);
        }
    }

}

void Statement() : {
} {
      ( LocalVariableDeclaration() | Command() | Macro() )? <EOS>
}

void MacroSignature() : {
    Token name;
    Token id;
}{
    name = <IDENTIFIER> "(" (<NUMBER> | id = <IDENTIFIER> { macroVariables.add(id.image); } )?
            ("," (<NUMBER> | id = <IDENTIFIER> { macroVariables.add(id.image); } ) )* ")" {
        if( !preflight ) {
            throw new ParseException( "Macro substitution failed!\n" + getImage(name));
        }
    }
}

void Macro() : {
    Token name;
    Token id;
}{
    name = <IDENTIFIER> "(" (<NUMBER> | id = <IDENTIFIER> {
            if( dataSegment.getVariable(id.image) == null && !macroVariables.contains(id.image) ) throw new ParseException("Unknown variable " + id.image + " in line " + id.beginLine );
        } ( "[" <NUMBER> "]" )? )?
            ("," (<NUMBER> | id = <IDENTIFIER> {
            if( dataSegment.getVariable(id.image) == null && !macroVariables.contains(id.image) ) throw new ParseException("Unknown variable " + id.image + " in line " + id.beginLine );
        } ( "[" <NUMBER> "]" )? ) )* ")" {
            if( !preflight ) {
                throw new ParseException( "Macro substitution failed!\n" + getImage(name));
            }
    }
}

void LocalVariableDeclaration() : {
    Token id, rvalue, size, index;
    int length = 1;
    int offset = 0;
} {
    <VAR> id = <IDENTIFIER> { macroVariables.add(id.image); } ( "[" size = <NUMBER> "]" { length = Integer.parseInt(size.image); } )? <ASSIGN> (
          rvalue = <NUMBER> {
            if( !preflight ) {
                Number base = dataSegment.newVariable(id.image, length, rvalue.image);
                int value = Integer.parseInt(rvalue.image);
                for( int j = 0; j < length; ++j ) {
                    Number variable = base.offset(j);
                    codeSegment.addClear(variable);
                    for( int i = 0; i < value; ++i ) {
                        codeSegment.addINC(variable);
                    }
                }
            }
        }
        | rvalue = <IDENTIFIER> ( "[" index = <NUMBER> "]" { offset = Integer.parseInt(index.image); } )? {
            if( !preflight ) {
                Number left   = dataSegment.newVariable(id.image, length, 0);
                Number middle = dataSegment.newVariable(0);
                Number right  = dataSegment.getVariable(rvalue.image, offset);
                if( right == null ) {
                    if( !macroVariables.contains(rvalue.image) ) {
                        throw new ParseException("Unknown variable " + rvalue.image + " in line " + rvalue.beginLine );
                    } else {
                        right = new Number(0);
                    }
                }
                for( int j = 0; j < length; ++j ) {
                    codeSegment.addClear(left.offset(j));
                    codeSegment.addClear(middle);
                    codeSegment.addINC(right);
                    Number cloneLoop = codeSegment.newLabel();
                    codeSegment.addINC(left.offset(j));
                    codeSegment.addINC(middle);
                    codeSegment.addDEC(right);
                    codeSegment.addJNZ(right, cloneLoop);
                    codeSegment.addDEC(left.offset(j));
                    Number restoreLoop = codeSegment.newLabel();
                    codeSegment.addINC(right);
                    codeSegment.addDEC(middle);
                    codeSegment.addJNZ(middle, restoreLoop);
                    codeSegment.addDEC(right);
                }
            }
        }
    )
}

void Command() : {
    Number arg1, arg2;
} {

      Halt()
    | Nop()
    | Inc()
    | Dec()
    | Jnz()
}

Number DoWhileCondition() : {
    Number address;
    Token number;
} {
    address = Lhs() (
        <NEQ> number = <NUMBER> {
            int n = Integer.parseInt(number.image);
            if( n == 0 ) {
                return address;
            } else {
                return address;
            }
        }
        | <EQ> number = <NUMBER> {
            Number target = codeSegment.newLabel();
            Number flag = dataSegment.newVariable(0);
            codeSegment.addClear(flag);
            codeSegment.addJNZ(address, target);
            codeSegment.addINC(flag);
            codeSegment.placeLabel(target);
            return flag;
        }
    )
}

Number Lhs() : {
    Token lhs, index;
    int offset = 0;
} {
      lhs = <IDENTIFIER> ( "[" index = <NUMBER> "]" { offset = Integer.parseInt(index.image); } )? {
        Number left = dataSegment.getVariable(lhs.image, offset);
        if( left == null ) {
            if( !macroVariables.contains(lhs.image) ) {
                throw new ParseException("Unknown variable " + lhs.image + " in line " + lhs.beginLine );
            } else {
                left = new Number(0);
            }
        }
        return left;
      }
    | lhs = <NUMBER> {
        return dataSegment.newVariable(Integer.parseInt(lhs.image));
    }
}

Number IfCondition() : {
    Number address;
    Token number;
    Token constant;
} {
      address = Lhs() (
          <NEQ> number = <NUMBER> {
            Number target = codeSegment.newLabel();
            Number flag = dataSegment.newVariable(0);
            codeSegment.addClear(flag);
            codeSegment.addJNZ(address, target);
            codeSegment.addINC(flag);
            codeSegment.placeLabel(target);
            return flag;
        }
        | <EQ> number = <NUMBER> {
            return address;
        }
    )
}


Number Variable() : {
    Token id, index;
    int offset = 0;
} {
    id = <IDENTIFIER> ( "[" index = <NUMBER> "]" { offset = Integer.parseInt(index.image); } )? {
        Number val = dataSegment.getVariable(id.image, offset);
        if( val == null ) {
            if( !macroVariables.contains(id.image) ) {
                throw new ParseException("Unknown variable " + id.image + " in line " + id.beginLine );
            } else {
                val = new Number(0);
            }
        }
        return val;
    }
}

void Halt() : {
} {
    <HALT> { codeSegment.add(Number.HALT); }
}

void Nop() : {
} {
    <NOP> { codeSegment.add(Number.NOP); }
}

void Inc() : {
    Number arg1;
} {
    <INC> arg1 = Variable() { codeSegment.addINC(arg1); }
}

void Dec() : {
    Number arg1;
} {
    <DEC> arg1 = Variable() { codeSegment.addDEC(arg1); }
}

void Jnz() : {
    Number arg1;
    Token arg2;
} {
    <JNZ> arg1 = Variable() arg2 = <IDENTIFIER> {
        Number target = codeSegment.getLabel(arg2.image);
        codeSegment.addJNZ(arg1, target);
    }
}
